plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlinx-serialization'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'jsonschema2pojo'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "ro.sts.dgc"
        minSdkVersion 26
        targetSdkVersion 31
        versionCode 18
        versionName "1.0.18"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        buildConfigField("java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)")
    }

    signingConfigs {
        config {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        debug {

        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    flavorDimensions "environment"
    productFlavors {
        create("preprod") {
            dimension = "environment"
            applicationId "ro.sts.dgc.preprod"
            versionNameSuffix = "-preprod"

            buildConfigField("String", "BASE_URL", "\"https://preprod-dgc.gov.ro/\"")
        }
        create("production") {
            dimension = "environment"
            applicationId "ro.sts.dcc"

            buildConfigField("String", "BASE_URL", "\"https://dgc.gov.ro/\"")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    packagingOptions {
        pickFirst 'META-INF/ASL-2.0.txt'
        pickFirst 'draftv4/schema'
        pickFirst 'draftv3/schema'
        pickFirst 'META-INF/LICENSE'
        pickFirst 'META-INF/LGPL-3.0.txt'
    }
}

dependencies {

    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.5.31'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.5.31'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.5.31'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-cbor:1.3.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "androidx.appcompat:appcompat:1.3.1"
    implementation 'androidx.emoji:emoji:1.1.0'
    implementation "com.google.android.material:material:1.4.0"

    implementation "androidx.navigation:navigation-fragment-ktx:2.3.5"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.5"

    implementation 'com.google.dagger:hilt-android:2.40'
    kapt 'com.google.dagger:hilt-compiler:2.40'
    implementation 'androidx.work:work-runtime-ktx:2.7.0'

    implementation "androidx.hilt:hilt-work:1.0.0"
    kapt "androidx.hilt:hilt-compiler:1.0.0"

    implementation "androidx.room:room-runtime:2.4.0-beta01"
    kapt "androidx.room:room-compiler:2.4.0-beta01"
    implementation("androidx.room:room-ktx:2.4.0-beta01")

    implementation("androidx.lifecycle:lifecycle-livedata-ktx:2.4.0")

    // Play Core Library
    implementation 'com.google.android.play:core:1.10.2'
    implementation 'com.google.android.play:core-ktx:1.8.1'

    implementation "com.jakewharton.timber:timber:5.0.1"

    implementation 'org.bouncycastle:bcpkix-jdk15to18:1.69'

    implementation "com.augustcellars.cose:cose-java:1.1.0"

    implementation "org.glassfish:javax.annotation:10.0-b28"
    implementation "javax.validation:validation-api:2.0.1.Final"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.13.0"
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.13.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.0'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.0'

    implementation "com.github.java-json-tools:json-schema-validator:2.2.14"

    implementation 'com.journeyapps:zxing-android-embedded:4.3.0'

    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.retrofit2:converter-jackson:2.9.0"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

jsonSchema2Pojo {
    source = files("${project.rootDir}/schema")
    removeOldOutput = true
    targetPackage = 'ro.sts.dgc.schema.payload.v1'
    includeGeneratedAnnotation = false
    includeAdditionalProperties = false
    useTitleAsClassname = true
    includeJsr303Annotations = true
    includeJsr305Annotations = true
    initializeCollections = false
    includeConstructors = true
    generateBuilders = true
    dateType = "java.time.LocalDate"
    dateTimeType = "java.time.Instant"
}